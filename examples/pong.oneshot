# Global variables
LET BALLSIZE = 8
LET BALLXSPEED = 2
LET BALLYSPEED = 2
LET BALLX = 150
LET BALLY = 75

LET PADDLEWIDTH = 8
LET PADDLEHEIGHT = 40

LET PADDLE1X = 10
LET PADDLE1Y = 55

LET PADDLE2X = 280
LET PADDLE2Y = 55

LET PADDLESPEED = 2

LET PLAYER1SCORE = 0
LET PLAYER2SCORE = 0

LET RUNNING = TRUE

# Initialization
WINDOW 300, 150

# Game loop
WHILE (RUNNING) THEN
    # Rendering
    COLOR "BLACK"
    FILL ALL

    COLOR "WHITE"
    FILL BALLX, BALLY, BALLSIZE, BALLSIZE
    FILL PADDLE1X, PADDLE1Y, PADDLEWIDTH, PADDLEHEIGHT
    FILL PADDLE2X, PADDLE2Y, PADDLEWIDTH, PADDLEHEIGHT

    BALLX = BALLX + BALLXSPEED
    BALLY = BALLY + BALLYSPEED

    TEXT 120, 40, PLAYER1SCORE
    TEXT 180, 40, PLAYER2SCORE

    # Collisions
    IF (BALLY > 150 - BALLSIZE OR BALLY <= 0) THEN
        BALLYSPEED = BALLYSPEED * (-1)
    END

    IF (BALLX > 300) THEN
        PRINT "Player 1 scores"
        BALLX = 150
        BALLY = 75
        PLAYER1SCORE = PLAYER1SCORE + 1
    END

    IF (BALLX + BALLSIZE < 0) THEN
        PRINT "Player 2 scores"
        BALLX = 150
        BALLY = 75
        PLAYER2SCORE = PLAYER2SCORE + 1
    END

    IF (COLLIDE(BALLX, BALLY, BALLSIZE, BALLSIZE, PADDLE1X, PADDLE1Y, PADDLEWIDTH, PADDLEHEIGHT)) THEN
        BALLXSPEED = BALLXSPEED * (-1)
    END

    IF (COLLIDE(BALLX, BALLY, BALLSIZE, BALLSIZE, PADDLE2X, PADDLE2Y, PADDLEWIDTH, PADDLEHEIGHT)) THEN
        BALLXSPEED = BALLXSPEED * (-1)
    END

    # Input
    IF (INPUT("W")) THEN
        PADDLE1Y = PADDLE1Y - PADDLESPEED
    END
    IF (INPUT("S")) THEN
        PADDLE1Y = PADDLE1Y + PADDLESPEED
    END

    # Input
    IF (INPUT("ARROWUP")) THEN
        PADDLE2Y = PADDLE2Y - PADDLESPEED
    END
    IF (INPUT("ARROWDOWN")) THEN
        PADDLE2Y = PADDLE2Y + PADDLESPEED
    END

    SLEEP 1000 / 60
END